USE [IT3020_INTEGRACAO]
GO
/****** Object:  StoredProcedure [dbo].[SEL_DADOS_REQUISICAO_SEGURO]    Script Date: 27/07/2023 14:09:46 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER   PROCEDURE [dbo].[SEL_DADOS_REQUISICAO_SEGURO](@IdCotacao VARCHAR(15), @IdContrato  VARCHAR(15), @NumProposta INT)
AS
BEGIN
/*
==============================================================================================
   Nome:	    SEL_DADOS_REQUISICAO_SEGURO
   Base:        CON_WEB_IT3020_INTEGRACAO
   Projeto:     TOYOTA CORRETORA / SEGURO AUTO
   Descricao:	PROCEDURE QUE BUSCA DADOS DE REQUISICOES DE SEGURO PARA ENVIO PARA A QUIVER.
				UTILIZADA NO SERVICO TIB_SEGUROS

   Versao Data		   Autor					  Descricao
   ------------------------------------------------------------------------------- 
   
   V1.0	  26/06/2023  Wang						  Versao Inicial 
   
==============================================================================================
*/

	SET NOCOUNT ON 

	DECLARE @ENDRES_RUA VARCHAR(40)
		,@ENDRES_NUMERO VARCHAR(10)
		,@ENDRES_COMPLEMENTO VARCHAR(40)
		,@ENDRES_BAIRRO VARCHAR(20)
		,@ENDRES_CIDADE VARCHAR(25)
		,@ENDRES_UF VARCHAR(2)
		,@ENDRES_CEP VARCHAR(8)
		,@DDD_TEL_RES VARCHAR(4)
		,@TEL_RES VARCHAR(12)
		,@DDD_TEL_COM VARCHAR(4)
		,@TEL_COM VARCHAR(12)
		,@DDD_TEL_CEL VARCHAR(4)
		,@TEL_CEL VARCHAR(12)

	-- BUSCA DADOS DE ENDERECO RESIDENCIAL DO CLIENTE
	SELECT TOP 1 
		@ENDRES_RUA = [dbo].[REMOVE_CARACTERES_ESPECIAIS](RTRIM(LTRIM(ISNULL(E.ENDERECO_SEM_NRO,''))))
		,@ENDRES_NUMERO = RTRIM(LTRIM(ISNULL(E.NUMERO,'')))		
		,@ENDRES_COMPLEMENTO = [dbo].[REMOVE_CARACTERES_ESPECIAIS](RTRIM(LTRIM(ISNULL(E.COMPLEMENTO,''))))
		,@ENDRES_BAIRRO = [dbo].[REMOVE_CARACTERES_ESPECIAIS](RTRIM(LTRIM(ISNULL(E.BAIRRO,''))))
		,@ENDRES_CIDADE = [dbo].[REMOVE_CARACTERES_ESPECIAIS](RTRIM(LTRIM(ISNULL(E.CIDADE,''))))
		,@ENDRES_UF = RTRIM(LTRIM(ISNULL(E.UF,'')))
		,@ENDRES_CEP = RTRIM(LTRIM(ISNULL(E.CEP,'')))
		-- TRATA TELEFONE RESIDENCIAL
		,@DDD_TEL_RES =
			CASE
				WHEN  LEN(RTRIM(LTRIM(E.TELEFONE))) > 9 THEN LEFT(RTRIM(LTRIM(E.TELEFONE)),2)
				ELSE ''
			END 
	   ,@TEL_RES = 
			CASE
				WHEN  LEN(RTRIM(LTRIM(E.TELEFONE))) >= 11 THEN RIGHT(RTRIM(LTRIM(E.TELEFONE)),9)
				WHEN  LEN(RTRIM(LTRIM(E.TELEFONE))) = 10 THEN RIGHT(RTRIM(LTRIM(E.TELEFONE)),8)
				ELSE ISNULL(RTRIM(LTRIM(E.TELEFONE)),'')
			END 
		-- TRATA TELEFONE CELULAR
	   ,@DDD_TEL_CEL = 
		   CASE
				WHEN  LEN(RTRIM(LTRIM(E.CELULAR))) > 9 THEN LEFT(RTRIM(LTRIM(E.CELULAR)),2)
				ELSE ''
			END  
	   ,@TEL_CEL = 
		   CASE
				WHEN  LEN(RTRIM(LTRIM(E.CELULAR))) >= 11 THEN RIGHT(RTRIM(LTRIM(E.CELULAR)),9)
				WHEN  LEN(RTRIM(LTRIM(E.CELULAR))) = 10 THEN RIGHT(RTRIM(LTRIM(E.CELULAR)),8)
				ELSE ISNULL(RTRIM(LTRIM(E.CELULAR)),'')
			END  
		FROM AB_BATIVOS..PROPOSTA_COTACAO_SEGURO S (NOLOCK) 
		INNER JOIN AB_OPEVAREJO..PROPOSTAS P (NOLOCK) 
			ON S.NROPROPOSTA = P.NROPROPOSTA 
			AND S.IDCONTRATO != ''
		INNER JOIN AB_INFOBANC..ENDERECOS E (NOLOCK) 
		ON P.CNPJCPF = E.CGC_CPF AND E.TIPOENDERECO='RESIDENCIAL'	
		WHERE S.IDCOTACAO = @IdCotacao 
			AND S.IDCONTRATO = @IdContrato
			AND S.NROPROPOSTA = @NumProposta
		ORDER BY E.CORRESPONDENCIA DESC, E.CODENDERECO DESC

	-- BUSCA DADOS DE ENDERECO COMERCIAL DO CLIENTE
	SELECT TOP 1		 
	--TRATA TELEFONE COMERCIAL
	   @DDD_TEL_COM = 
		   CASE
				WHEN  LEN(RTRIM(LTRIM(E.TELEFONE))) > 9 THEN ISNULL(LEFT(RTRIM(LTRIM(E.TELEFONE)),2),'')
				ELSE ''
			END  
	   ,@TEL_COM = 
		   CASE
				WHEN  LEN(RTRIM(LTRIM(E.TELEFONE))) >= 11 THEN RIGHT(RTRIM(LTRIM(E.TELEFONE)),9)
				WHEN  LEN(RTRIM(LTRIM(E.TELEFONE))) = 10 THEN RIGHT(RTRIM(LTRIM(E.TELEFONE)),8)
				ELSE ISNULL(RTRIM(LTRIM(E.TELEFONE)),'')
			END  
		FROM AB_BATIVOS..PROPOSTA_COTACAO_SEGURO S (NOLOCK) 
		INNER JOIN AB_OPEVAREJO..PROPOSTAS P (NOLOCK) 
			ON S.NROPROPOSTA = P.NROPROPOSTA 
			AND S.IDCONTRATO != ''
		INNER JOIN AB_INFOBANC..ENDERECOS E (NOLOCK) 
		ON P.CNPJCPF = E.CGC_CPF AND E.TIPOENDERECO='COMERCIAL'	
		WHERE S.IDCOTACAO = @IdCotacao 
			AND S.IDCONTRATO = @IdContrato
			AND S.NROPROPOSTA = @NumProposta
		ORDER BY E.CORRESPONDENCIA DESC, E.CODENDERECO DESC

	SELECT  S.IDCOTACAO  
			,S.IDCONTRATO 
			,S.NROPROPOSTA
			,[dbo].[REMOVE_CARACTERES_ESPECIAIS](RTRIM(LTRIM(CLI.NOME))) AS NOME_CLIENTE
			,RTRIM(LTRIM(CLI.CGC_CPF)) as CGC_CPF
			,CLI.TIPOPESSOA
			,CLI.SEXO
			,CONVERT(CHAR(10),CLI.DATANASCIMENTO,126) AS DAT_NASC
			,RTRIM(LTRIM(ISNULL(CLI.EMAIL,''))) AS EMAIL_CLIENTE
		    ,RTRIM(LTRIM(CLI.NUMDOCUMENTO)) AS DOCIDENTCLIENTE
		    ,RTRIM(LTRIM(CLI.TIPODOCUMENTO)) AS DESCRTIPOIDENT
			,RTRIM(LTRIM(CLI.ORGAOEXPEDIDOR)) AS ORGAOEXPEDCLIENTE
			,RTRIM(LTRIM(CLI.ESTCIVIL)) AS ESTCIVIL
			,'Residencial' AS TIPO_ENDERECO
			,@ENDRES_RUA AS ENDRES_RUA			
			,@ENDRES_NUMERO AS ENDRES_NUMERO
			,@ENDRES_COMPLEMENTO AS ENDRES_COMPLEMENTO 
			,@ENDRES_BAIRRO AS ENDRES_BAIRRO
			,@ENDRES_CIDADE AS ENDRES_CIDADE
			,@ENDRES_UF AS ENDRES_UF
			,@ENDRES_CEP AS ENDRES_CEP
			,@DDD_TEL_RES AS DDD_TEL_RES
			,@TEL_RES AS TEL_RES
			,@DDD_TEL_COM AS DDD_TEL_COM
			,@TEL_COM AS TEL_COM
			,@DDD_TEL_CEL AS DDD_TEL_CEL
			,@TEL_CEL AS TEL_CEL
			-- DADOS DO SEGURO
			,CONVERT(CHAR(10),S.DATAHORA,126) AS DATA_PROPOSTA
			,CONVERT(CHAR(10),S.DATAINICIO,126) AS DATA_INI_VIGENCIA
			,CONVERT(CHAR(10),S.DATAFIM,126) AS DATA_FIM_VIGENCIA
			,QTD_PARC = S.PRAZOSEG
		   ,RTRIM(LTRIM(ISNULL(M.DESMARCA,''))) AS MARCA    
		   ,RTRIM(LTRIM(M.DESMODELO)) AS MODELO
		   ,S.VLRLIQCOTACAO AS VLR_PREMIO_LIQUIDO 
		   ,S.VLRIOFCOTACAO	AS VLR_IOF
		   ,PERC_IOF = CONVERT(NUMERIC(7,5), S.VLRIOFCOTACAO / S.VLRLIQCOTACAO)
		   ,S.VLRTOTALCOTACAO AS VLR_PREMIO_TOTAL
		   ,S.ANOFABRICACAO
		   ,S.ANOMODELO 
		   ,S.ZEROKM  
		   ,B.PLACA
		   ,B.UFPLACA
		   ,B.CHASSI
		   ,B.COR
		   ,B.COMBUSTIV 
		   ,B.RENAVAN 
		   
		   ,'Autom√≥vel individual'  AS RAMO
		   ,ISNULL(PS.NOME,'')  AS DESC_SEGURADORA   
		   -- DADOS DO FINANCIAMENTO
		   ,P.QTDPARCELAS
		   ,P.DATVENCTO
		   ,P.DATPRIMVENCTO 
		   ,P.VLRFINANCIADO
		   ,P.VLRPRINCIPAL
		   ,P.VLRENTRADA 
		   ,P.VLRPARCELA 
		   --,CONVERT(CHAR(10),CON.DAT_CONT,126) AS DATA_VENDA
	       --,CONVERT(CHAR(10),DATEADDNI) AS DIA_VCTO_PARCELAS  --Dia vencimento parcelas
	FROM AB_BATIVOS..PROPOSTA_COTACAO_SEGURO S (NOLOCK) 
	INNER JOIN AB_OPEVAREJO..PROPOSTAS P (NOLOCK) 
		ON S.NROPROPOSTA = P.NROPROPOSTA 
		AND S.IDCONTRATO != ''
	INNER JOIN AB_INFOBANC..PESSOAS CLI (NOLOCK) 
		ON P.CNPJCPF = CLI.CGC_CPF
	INNER JOIN AB_INFOBANC..PESSOAS PS (NOLOCK) 
		ON PS.CGC_CPF = S.CNPJSEGURADORA
	INNER JOIN AB_BATIVOS..GARANTIAS_BENS B
		ON S.NROPROPOSTA = B.NROPROPOSTA
		AND CODGAR = 'ALIENACAO'
	INNER JOIN AB_BATIVOS..MARCAS_MODELOS M
		ON S.CODMODELO = M.CODMERCADO
	WHERE S.IDCOTACAO = @IdCotacao 
	AND S.IDCONTRATO = @IdContrato
	AND S.NROPROPOSTA = @NumProposta
	order by S.IDCOTACAO desc


END
